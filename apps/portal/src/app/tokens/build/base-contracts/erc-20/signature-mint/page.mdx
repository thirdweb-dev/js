import { Details } from "@doc";
import { GithubButtonLink } from "@doc";
import { createMetadata } from "@doc";

export const metadata = createMetadata({
	image: {
		title: "ERC20SignatureMint contract",
		icon: "contract",
	},
	title: "ERC20SignatureMint | thirdweb contracts",
	description:
		"The ERC20SignatureMint smart contract adds the signature-based minting extension functionality to the ERC20Base contract",
});

# ERC20SignatureMint

The `ERC20SignatureMint` smart contract adds the [signature-based minting extension](/contracts/build/extensions/erc-20/ERC20signaturemint)
functionality to the [ERC20Base](/contracts/build/base-contracts/erc-20/base) contract. [Signature minting](/glossary/signature-based-minting) uses [EIP-712](https://eips.ethereum.org/EIPS/eip-712),
which enables the contract admin to authorize an external party's request to mint tokens on the admin's contract.
At a high level, this means you can authorize others to mint tokens
on your contract, and specify what exactly will be minted by that external party.

```solidity
import "@thirdweb-dev/contracts/base/ERC20SignatureMint.sol";
```

<GithubButtonLink href="https://github.com/thirdweb-dev/contracts/blob/main/contracts/base/ERC20SignatureMint.sol" />

## Usage

Import the contract and inherit from it.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@thirdweb-dev/contracts/base/ERC20SignatureMint.sol";

contract MyToken is ERC20SignatureMint {
	  constructor(
        address _defaultAdmin,
        string memory _name,
        string memory _symbol,
        address _primarySaleRecipient
    )
        ERC20SignatureMint(
            _defaultAdmin,
            _name,
            _symbol,
            _primarySaleRecipient
        )
    {}
}
```

## Detected Extensions

Once deployed, you can use the features made available by these extensions on the SDK and dashboard:

- [ERC20](/contracts/build/extensions/erc-20/ERC20)
- [ERC20Mintable](/contracts/build/extensions/erc-20/ERC20BatchMintable)
- [ERC20BatchMintable](/contracts/build/extensions/erc-20/ERC20BatchMintable)
- [ERC20SignatureMintable](/contracts/build/extensions/erc-20/ERC20signaturemint)
- [ERC20Permit](/contracts/build/extensions/erc-20/ERC20Permit)
- [PrimarySale](/contracts/build/extensions/general/PrimarySale)
- [ContractMetadata](/contracts/build/extensions/general/ContractMetadata)
- [Ownable](/contracts/build/extensions/general/Ownable)

## Functions to Override

The following functions have been implemented on this contract & are available to be overridden to add custom logic:

<Details id="mintWithSignature" summary="mintWithSignature">

```solidity
/**
 *  @notice           Mints tokens according to the provided mint request.
 *
 *  @param _req       The payload / mint request.
 *  @param _signature The signature produced by an account signing the mint request.
 */
function mintWithSignature(MintRequest calldata _req, bytes calldata _signature)
    external
    payable
    virtual
    returns (address signer)
{
    require(_req.quantity > 0, "Minting zero tokens.");

    // Verify and process payload.
    signer = _processRequest(_req, _signature);

    address receiver = _req.to;

    // Collect price
    _collectPriceOnClaim(_req.primarySaleRecipient, _req.quantity, _req.currency, _req.pricePerToken);

    // Mint tokens.
    _mint(receiver, _req.quantity);

    emit TokensMintedWithSignature(signer, receiver, _req);
}
```

</Details>

<Details id="_collectPriceOnClaim" summary="_collectPriceOnClaim">

```solidity
/// @dev Collects and distributes the primary sale value of tokens being claimed.
function _collectPriceOnClaim(
    address _primarySaleRecipient,
    uint256 _quantityToClaim,
    address _currency,
    uint256 _pricePerToken
) internal virtual {
    if (_pricePerToken == 0) {
        return;
    }

    uint256 totalPrice = (_quantityToClaim * _pricePerToken) / 1 ether;
    require(totalPrice > 0, "quantity too low");

    if (_currency == CurrencyTransferLib.NATIVE_TOKEN) {
        require(msg.value == totalPrice, "Must send total price.");
    }

    address saleRecipient = _primarySaleRecipient == address(0) ? primarySaleRecipient() : _primarySaleRecipient;
    CurrencyTransferLib.transferCurrency(_currency, msg.sender, saleRecipient, totalPrice);
}
```

</Details>
