import { createMetadata, Steps, Step } from "@doc";

export const metadata = createMetadata({
	title: "thirdweb Modular Contracts",
	description:
		"Modular Contract provides a framework to build custom smart contracts more efficiently by offering a set of pre-built base contracts and reusable components, better known as modules.",
	image: {
		title: "thirdweb Modular Contracts",
		icon: "solidity",
	},
});

# Crear Contrato de MÃ³dulo
**Requisito previo:** Lee â€œCrear Contrato Principalâ€ antes de crear un MÃ³dulo.

### InstalaciÃ³n

<Steps>

<Step title="Instalar Forge desde Foundry">
   Para obtener ayuda, consulta la [guÃ­a de instalaciÃ³n de Foundry](https://book.getfoundry.sh/getting-started/installation).

```bash
forge init
forge install thirdweb-dev/modular-contracts --no-commit
```

AÃ±adir los contratos modulares de Thirdweb a `foundry.toml` bajo `remappings`:

```toml
remappings = ['@thirdweb-dev=lib/modular-contracts/']
```

</Step>

### Crear MÃ³dulo

<Step title="Crear un Nuevo Archivo de MÃ³dulo">
   Crea un nuevo archivo llamado `CounterModule.sol` en la carpeta `src` y comienza con el siguiente cÃ³digo:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {Module} from "@thirdweb-dev/src/Module.sol";

contract CounterModule is Module {
    function getModuleConfig()
        public
        pure
        override
        returns (ModuleConfig memory config)
    {}
}
```

> **Note:**
> El contrato `Module` es el contrato base que debe ser heredado para que este contrato sea reconocido como un Contrato de MÃ³dulo, y necesitamos implementar la funciÃ³n `getModuleConfig` para evitar que el contrato sea marcado como abstracto.

</Step>

<Step title="Crear una LibrerÃ­a de Almacenamiento">
   Crea una librerÃ­a llamada `CounterStorage` responsable de mantener el estado del Contrato de MÃ³dulo:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {Module} from "@thirdweb-dev/src/Module.sol";

contract CounterModule is Module {
    function getModuleConfig()
        public
        pure
        override
        returns (ModuleConfig memory config)
    {}
}

 // ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡
library CounterStorage {
    /// @custom:storage-location erc7201:token.minting.counter
    bytes32 public constant COUNTER_STORAGE_POSITION =
        keccak256(abi.encode(uint256(keccak256("counter")) - 1)) &
            ~bytes32(uint256(0xff));

    struct Data {
        uint256 step;
    }

    function data() internal pure returns (Data storage data_) {
        bytes32 position = COUNTER_STORAGE_POSITION;
        assembly {
            data_.slot := position
        }
    }
}

```

> **Nota:**
> La librerÃ­a `CounterStorage` utiliza el diseÃ±o de almacenamiento de espacio de nombres ERC-7201 para almacenar los datos. Aprende mÃ¡s sobre [ERC-7201](https://eips.ethereum.org/EIPS/eip-7201).

</Step>

<Step title="Configurar FunciÃ³n de Acceso a Almacenamiento">
   Configura la funciÃ³n `_counterStorage` para acceder al almacenamiento desde la librerÃ­a `CounterStorage`:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {Module} from "@thirdweb-dev/src/Module.sol";

contract CounterModule is Module {
    function getModuleConfig()
        public
        pure
        override
        returns (ModuleConfig memory config)
    {}

     // ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡

    function _counterStorage()
        internal
        pure
        returns (CounterStorage.Data storage)
    {
        return CounterStorage.data();
    }
}

library CounterStorage {
    /// @custom:storage-location erc7201:token.minting.counter
    bytes32 public constant COUNTER_STORAGE_POSITION =
        keccak256(abi.encode(uint256(keccak256("counter")) - 1)) &
            ~bytes32(uint256(0xff));

    struct Data {
        uint256 step;
    }

    fun2ction data() internal pure returns (Data storage data_) {
        bytes32 position = COUNTER_STORAGE_POSITION;
        assembly {
            data_.slot := position
        }
    }
}

```

</Step>

<Step title="Configurar Funciones de Fallback">
   Configura funciones de fallback que actÃºan como los setters y getters para `step`:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {Module} from "@thirdweb-dev/src/Module.sol";

contract CounterModule is Module {
    function getModuleConfig()
        public
        pure
        override
        returns (ModuleConfig memory config)
    {}

    function _counterStorage()
        internal
        pure
        returns (CounterStorage.Data storage)
    {
        return CounterStorage.data();
    }

    // ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡
    // Fallback Functions
    function getStep() external view returns (uint256) {
        return _counterStorage().step;
    }

    function setStep(uint256 _step) external {
        _counterStorage().step = _step;
    }
}

library CounterStorage {
    /// @custom:storage-location erc7201:token.minting.counter
    bytes32 public constant COUNTER_STORAGE_POSITION =
        keccak256(abi.encode(uint256(keccak256("counter")) - 1)) &
            ~bytes32(uint256(0xff));

    struct Data {
        uint256 step;
    }

    function data() internal pure returns (Data storage data_) {
        bytes32 position = COUNTER_STORAGE_POSITION;
        assembly {
            data_.slot := position
        }
    }
}

```

> **Note:**
> Las funciones de fallback son funcionalidades adicionales que un contrato central puede usar a travÃ©s de la funciÃ³n `fallback` de Solidity.

</Step>

<Step title="Configurar la FunciÃ³n de Callback">
   Configura una funciÃ³n de callback `beforeIncrement` que aumente el contador dado por `step`:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {Module} from "@thirdweb-dev/src/Module.sol";

contract CounterModule is Module {
    function getModuleConfig()
        public
        pure
        override
        returns (ModuleConfig memory config)
    {}

    function _counterStorage()
        internal
        pure
        returns (CounterStorage.Data storage)
    {
        return CounterStorage.data();
    }

    // Fallback Functions
    function getStep() external view returns (uint256) {
        return _counterStorage().step;
    }

    function setStep(uint256 _step) external {
        _counterStorage().step = _step;
    }


    // ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡
    function beforeIncrement(uint256 count) external view returns (uint256) {
        return count + _counterStorage().step;
    }
}

library CounterStorage {
    /// @custom:storage-location erc7201:token.minting.counter
    bytes32 public constant COUNTER_STORAGE_POSITION =
        keccak256(abi.encode(uint256(keccak256("counter")) - 1)) &
            ~bytes32(uint256(0xff));

    struct Data {
        uint256 step;
    }

    function data() internal pure returns (Data storage data_) {
        bytes32 position = COUNTER_STORAGE_POSITION;
        assembly {
            data_.slot := position
        }
    }
}

```

> **Note:**
> Las funciones de callback son funcionalidades tipo hook que se pueden usar antes o despuÃ©s de la funcionalidad principal de un contrato central. En este fragmento, el callback `beforeIncrement` se utiliza antes de la funcionalidad principal de incremento.

</Step>

<Step title="Configurar el MÃ³dulo">
   Finalmente, configura la funcionalidad `getModuleConfig`, ya que esta es la encargada de comunicarse con el contrato central:

```solidity
//SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import {Module} from "@thirdweb-dev/src/Module.sol";
// ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡
import {Role} from "@thirdweb-dev/src/Role.sol";

contract CounterModule is Module {
    // ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡ğŸ‘‡
    function getModuleConfig()
        public
        pure
        override
        returns (ModuleConfig memory config)
    {
        // Callback Function array of one element
        config.callbackFunctions = new CallbackFunction[](1);
        // Fallback Function array of two elements
        config.fallbackFunctions = new FallbackFunction[](2);

        //adding the functions to the arrays
        config.callbackFunctions[0] = CallbackFunction(
            this.beforeIncrement.selector
        );

        config.fallbackFunctions[0] = FallbackFunction({
            selector: this.getStep.selector,
            permissionBits: 0
        });

        config.fallbackFunctions[1] = FallbackFunction({
            selector: this.setStep.selector,
            permissionBits: Role._MANAGER_ROLE
        });

        // Required interfaces for the Module
        config.requiredInterfaces = new bytes4[](1);
        config.requiredInterfaces[0] = 0x00000001;

        // register the intallation callback
        config.registerInstallationCallback = true;
    }

    function _counterStorage()
        internal
        pure
        returns (CounterStorage.Data storage)
    {
        return CounterStorage.data();
    }

    // Fallback Functions
    function getStep() external view returns (uint256) {
        return _counterStorage().step;
    }

    function setStep(uint256 _step) external {
        _counterStorage().step = _step;
    }

    function beforeIncrement(uint256 count) external view returns (uint256) {
        return count + _counterStorage().step;
    }
}

library CounterStorage {
    /// @custom:storage-location erc7201:token.minting.counter
    bytes32 public constant COUNTER_STORAGE_POSITION =
        keccak256(abi.encode(uint256(keccak256("counter")) - 1)) &
            ~bytes32(uint256(0xff));

    struct Data {
        uint256 step;
    }

    function data() internal pure returns (Data storage data_) {
        bytes32 position = COUNTER_STORAGE_POSITION;
        assembly {
            data_.slot := position
        }
    }
}

```

</Step>

</Steps>

---

En el siguiente tutorial, aprende cÃ³mo desplegar este contrato modular y adjuntarlo al contrato principal.